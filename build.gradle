import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar
import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id "com.vanniktech.maven.publish" version "0.28.0"
    id 'signing'
    id 'jacoco' // JaCoCo
    id 'com.github.kt3k.coveralls' version '2.12.0' // Coveralls
}

group = 'io.github.hee9841.excel'
version = '0.0.4'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar() // 소스 코드도 포함
}

compileJava {
    options.release = 8
    options.compilerArgs += ["-Xlint:-options"]
}


dependencies {
    //poi
    api 'org.apache.poi:poi:5.4.0'
    api 'org.apache.poi:poi-ooxml:5.4.0'

    //logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    testImplementation 'ch.qos.logback:logback-classic:1.3.15'


    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter:'

    // mockito
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.mockito:mockito-inline:4.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.11.0'
}

test {
    useJUnitPlatform()
}


javadoc {
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        addStringOption('Xdoclint:none', '-quiet')
    }
}


signing {
    useInMemoryPgpKeys(
            System.getenv("ORG_GRADLE_PROJECT_signingInMemoryKey"),
            System.getenv("ORG_GRADLE_PROJECT_signingInMemoryKeyPassword"),
            System.getenv("ORG_GRADLE_PROJECT_signingInMemoryKeyId")
    )
    sign publishing.publications
}

var groupId = "io.github.hee9841"
var artifactId = "excel-module"

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
    signAllPublications()
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    coordinates(groupId, artifactId, version)

    pom {
        name = artifactId
        description = "A Java library that helps you work with Excel files easily."
        inceptionYear = "2025"
        url = "https://github.com/hee9841/excel-module"

        licenses {
            license {
                name = 'Apache License 2.0'
                url = 'https://github.com/hee9841/excel-module/blob/dev/LICENSE'
            }
        }

        developers {
            developer {
                id = 'hee9841'
                name = 'SungHee Lee'
                email = 'lee98417716@gmail.com'
            }

        }

        scm {
            connection = 'scm:git:git://github.com/hee9841/excel-module.git'
            developerConnection = 'scm:git:ssh://github.com:hee9841/excel-module.git'
            url = 'https://github.com/hee9841/excel-module/tree/master'
        }
    }
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.jacocoTestReport {
    reports {
        xml.required.set(true)  // Coveralls needs XML report
        html.required.set(true) // HTML report
    }
}

tasks.coveralls {
    dependsOn tasks.jacocoTestReport // Ensure test report runs first
}
